
PRIMZAHLEN3.=	SEGM,

	EXTERN	A&FSP,
	EXTERN	S&WKERN,

	INDEX	20	(XFLAGS,XZAEHL,XW1,XW2),

	-- HIER BEGINNT DAS PROGRAMM --

	R&DRUCKE,

	DRUCK	2,

	XBA	VB616,
	SSR	6	16	-- STARTMELDUNG --

	B	(10000000)		-- MAXIMALE ZU BERECHNENDE PRIMZAHL = 10.000.000 --
	SH	A	1,
	C	ANZBITS		-- HALBE MAXIMALE PRIMZAHL --

	RT	AQ,
	LR	1A,
	DVD	(24)		-- 24 BITS PRO HALBWORT --
	AA	3,
	SH	A	1,
	C2	ANZGW		-- ANZAHL DAFÜR BENÖTIGTER GANZWORTE --

	
	BAR	1,
	SFB	A&FSP		-- IM FREISPEICHER BESORGEN --
	TRX	A	XFLAGS	-- ANF.ADR. FLAGSPEICHER --

INIT=	B	ANZBITS,
	SH	AL	1,
	LR	1Q,
	NRM	FG,
	GML	(7.03687441776641E13) 	 -- 2 ** 46 --
	SU	S&WKERN,
	AU	(9/0),
	SH	AR	10,
	ZTR	1A,
	AA	6,
	C	WURZEL		--	WURZEL := INTEGER(SQRT(FLOAT(SIZE*2))) + 6 ;	--

	SU	VORLOESCHEN		-- ALLE FLAGS VORBESETZEN MIT TRUE --

LOS=	BA	0,
	C	VI,

	BH	WURZEL,
SCHL1=	ASP	0/B		-- FOR I := 0 TO WURZEL DO			--
	SU	UPGET		-- 	IF FLAGS [I] THEN			--
	SI0	NOT1,

	BQB	VI		-- 		PRIME := I * 2 + 3 ;		--
	SH	AL	1,
	AA	3,
	C	VPRIME,

	R	A	Q	-- 		K := I + PRIME ;		--
	C	VK,

	BH	ANZBITS,
SCHL2=	ASP	0/B		--		WHILE K <= SIZE DO		--
	SU	UPLOESCH		--			FLAGS [K] := FALSE ;	--
	B	VPRIME,
	AC	VK		--			K := K + PRIME ;	--
	RT	AD,
	SKG	SCHL2		--		END ;			--	

	BH	WURZEL,

NOT1=	ASP	0/B		--	END ;				--

	BA	1,
	AC	VI,
	RT	AD,
	SKG	SCHL1		-- END ;					--

	BA	1000,
	C	VONNICHT,
	B	ANZBITS,
	SH	AL	1,
	SBA	1000,
	C	BISNICHT		-- VON 1000 .. N-1000 NICHT PROTOKOLLIEREN --

	LZL	0	1	-- VOR LÜCKE --

PRINT=	LC	VANZAHL		-- ANZAHL := 0 ;				--
	ZX	-9	XZAEHL,
	LR	1A,
	C	VI		-- FOR I := 0 TO SIZE DO			--
	BH	ANZBITS,
SCHL4=	SU	UPGET		-- 	IF FLAGS [I] THEN			--
	SN0	SCHL5,

	BA	1,
	AC	VI,
	RT	AD,
	SK	SCHL4,
	S	ESCHL4,

SCHL5=	BA	1,
	AC	VANZAHL		--		ANZAHL := ANZAHL + 1 ;		--

	B	VI		--		PRINT (I*2+3) ;			--
	SH	AL	1,
	AA	3,

	BH	VONNICHT,
	SK	PRINT3		-- VOR DER NICHT-AUSGABE-LÜCKE --

	SL	PRINT2	1,
	LZL	1	0	-- HIER 1.MAL IN DER LÜCKE --

	B	(''   ...''),
	R&AOKT	(6),
	S	PRINT4,

PRINT2=	BH	BISNICHT,
	SK	PRINT5		-- IN DER NICHT-AUSGABE-LÜCKE --

PRINT3=	ASP	0/B		-- HIER WIRD AUSGEGEBEN --

	R&AGANZ	(9),
	SZX	INPUFFER	XZAEHL,
PRINT4=	ZX	-9	XZAEHL,
	R&DRUCKE			--		ALLE 10 ZAHLEN NEUE ZEILE	--
INPUFFER=ASP	0/B,
PRINT5=	BH	ANZBITS,
	BA	1,
	AC	VI,
	RT	AD,
	SK	SCHL4		-- END ;					--

ESCHL4=	ASP	0/B,
	R&DRUCKE,
	R&DRUCKE,

	B	VANZAHL,
	R&AGANZ	(9),
	B	('' PRIMZ''),
	R&AOKT	(6),
	B	(''AHLEN ''),
	R&AOKT	(6),
	B	(''GESAMT''),
	R&AOKT	(6),
	B	('' UNTER''),
	R&AOKT	(6),

	B	ANZBITS		-- HALBE MAXIMALE PRIMZAHL --
	SH	AL	1,
	R&AGANZ	(9),

	R&DRUCKE,

--	KB	'4711'	--

	R&ENDE,


UPSETZ=	SU	BITADR,
	R	VEL	Q	-- OR MASKE --
	MAB	C2	0	-- UND SPEICHERN --
	MU	S	0,

UPLOESCH=	SU	BITADR,
	IR	Q,
	R	ET	Q	-- AND NOT MASKE --
	MAB	C2	0	-- UND SPEICHERN --
	MU	S	0,

UPGET=	SU	BITADR,
	R	ET	Q	-- AND MASKE --
	MU	S	0,

BITADR=	ASP	0/B		-- IN RA IST BITNUMMER, RH BLEIBT ERHALTEN --
	RT	AQ,
	LR	1A,
	DVD	(24),
	RX	A	XFLAGS	-- B = >HALBWORT< --
	BA	1,
	R	MCFU	Q	-- MIT DIVISIONSREST DAS BIT IM HALBWORT SHIFTEN --
	SH	AL	0,
	RT	AQ		-- Q = BITMASKE IM HALBWORT --
	MAB	B2	0	-- A = HALBWORT MIT BIT DRIN --
	MU	S	0,

VORLOESCHEN=ASP	0/B,
	XB	XFLAGS,
	XC	XW2,
	LR	3A,
	IR	A,
	MAB	C	0		-- 1. WORT MIT 'FFFFFF FFFFFF' LÖSCHEN --
	HBA	2,
	XC	XW1,
	TCB	ANZGW			-- ANZAHL GANZWORTE --
	VBA	1			--  -1	--
	WTV	XW1	XW2		-- ALLE LÖSCHEN --
	MU	S	0,

	R&ENDE,

ANZBITS=0/V			-- HALBE MAXIMALE ZU BERECHNENDE PRIMZAHL --
WURZEL=	0/V,
VONNICHT= 0/V,
BISNICHT= 0/V,

VI=	0/V,
VK=	0/V,
VPRIME=	0/V,
VANZAHL=	0/V,

ANZGW=	0/HV1,


VB616=	R&SSRFEHLER/AG, 3/H,
	TXT/AG, 10/H,

TXT=	''*020PRIMZAHLEN (FESTKOMMA-ARITHM.) MIT SIEB DES ERATOSTHENES :*020'', 0/2,

	ENDE,
