
PS&TEXTAUSGB=	SEGM			--PS&TEXTAUSGB: FEHLERTEXTOPERATOR BS3 --
	START	TX1,
	XBASIS	TXXB,
	UNTPR	XU,
	VORBES	(3,'000000000000'),
	ALARM	S&ALAD,
	EXTERN	S&SRF	(S&SRF,S&SRFR),
	EXTERN	S&ZEILBR (S&ZEILBR),
	EXTERN	S&ALARM	(S&ALARM,S&ALAD),
	INDEX	0	(XANF,XGW,XZ1,XZA,XZL,XZNR,XZR,XA,XB,XPP,XR,XL,XZBR,XU,XU1,XU2,XU3),
TX1=	B2	(TXF4/A),
	SU	S&ALARM,
	XBA	VBZUSS,
	SSR	0	22	--EER.AL.ZUSPS. SETZEN--
	B2	(TXFEND/A),
	SU	S&SRFR,
	XBA	VB18		--WAHLSCHALTER--
	SSR	1	8,
	SH	A	15,
	SRN	3R,
	LZL	2	0	-- GESPRAECH --
	S	2R,
	LZL	0	2	-- ABSCHNITT --
	XBA	VB40,
	SSR	4	0,
	SFB	S&ZEILBR,
	SN0	2R,
	BA	69,
	TRX	A	XZBR,
	TCB	TXSTKE+13 	-- ABSOLK --
	TBC	VB48+1,
	XBA	VB14,
	SSR	1	4,
	KE	INIT,
	XBA	S&DPUF,
	XC	XPP,
	B3	TXSTI+4		: AS ;
	SKG0	TXF1		-- AS KLEINER GLEICH 0 --
	BAR	3,
	SK	TXWARN,
	BZ	TFEHL		-- FEHLER: --
	S	TXAC,
TXWARN=	BA	5,
	SK	TXN,
	BZ	TWARN		-- WARNUNG: --
TXAC=	CZ	S&DPUF,
	XBA	S&DPUF+4,
	S	TXSW,
TXN=	BA	8,
	SK	TXF1		: AS > 8 ;
	B	(''*017''),
	C	S&DPUF,
	XBA	S&DPUF+2,
TXSW=	XC	XPP,
	RLR	S	H,
	S	TXA1,
	S	TXA2,
	S	TXA3,
	S	TXA2,
	S	TXA3,
	S	TXA2,
	S	TXA3,
	S	TXA11,

TXA1=	XBA	VB48		: AS=1 ;
	SSR	4	8,
TXA12=	ZX	69	XZBR,
	BZ	TXSTI+6,
	SU	TXAL1,
	S	TXA4,
				: AS = 8 , ALARMKELLER IM STARTSATZZ ;
TXA11=	XBA	TXSTI+6,
	XC	XANF,
	XBA	ALV3,
	XC	XGW,
	XBA	14,
	WTV	XGW	XANF,
	S	TXA12,

TXA2=	LZL	0	1	-- AS=2,4,6, STANDARDFEHLERTEXT --
	S	TXA21,
TXA3=	LZL	1	0	-- AS=3,4,7, STANDARDFEHLERTEXT UND EIGENER TEXT--
TXA21=	B	TXSTI+6,
	SH	AL	1,
	SH	ALK	7,
	LA	F		-- SS --
	SK0	TXF2		--F. SPRACHSCHL.--
	BAR	8,
	SK	TXF2,
	BZ	TXSTI+4,
	RT	AQ,
	SH	QK	8,
	R	B2	AL,
	SH	ZLK	32,
	ZTR	3A,
	RT	AD		-- SUCHKRITERIUM --
	B	('000000 FFFFFF'/3),
	XBA	2,
	RT	AH,
	R	MCFU	A,
	T	TXTDM,
	SAT	TXF3,
	SU	TXTTRANS 	-- TRANSPORT DES TEXTES IN AUSGABEPUFFER --
	SLN	TXA22	1	-- AS=2 --
	B2V	TXSTI+8		: PRUEFEN OB DYN.ANGEL.TEXT IN PUFFER PASST ;
	SH	A	15,
	SH	ZL	9,
	RX	A	XPP,
	VBA	PUFEND,
	SXK	TXA210,
	SU	ALDRUCK,
	B	(NL=''*021''),
	E	C	XPP,
	HXP	2	XPP,
TXA210=	BA	TXSTI+8,
	SU	TXTTRANS 	-- TRANSPORT DES ANGELIEFERTEN TEXTES --
TXA22=	SU	ALDRUCK		-- AUSDRUCKEN DES TEXTES --
TXA4=	XBA	VBZUSL,
	SSR	0	22	-- ER.-AL.-ZUSPS. LOESCHEN --
TXE=	ASP	0/B,
	KE	END,
	SSR	0	12,

TXF1=	SU	TXTFEHL		-- FALSCHER ARBEITSSCHLUESSEL AS --
	XBA	TXTF1,
TXF11=	BA	5,
	SU	ALWGT,
	SU	TXDRSTI,
	SU	TXDUMP,
	S	TXFENDN,
TXF2=	SU	TXTFEHL		-- FALSCHER SPRACHSCHLUESSEL SS --
	XBA	TXTF2,
	S	TXF11,
TXF3=	SU	TXTFEHL		-- TEXT NICHT GEFUNDEN --
	XBA	TXTF3,
	S	TXF11,
TXF4=	SI0	TXFE		-- EREIGNISALARM --
	SU	TXTFEHL,
	B	(''ALARM.''),
	E	C	XPP,
	HXP	2	XPP,
	SU	TXDRSTI,
TXFEND=	SU	TXDUMP,
TXFF=	ASP	0/B,
	KE	FEHL1,
	XBA	VB16,
	SSR	0	16	: REGW*EOLF ;
TXFENDN=	ASP	0/B,
	KE	FEHL2,
	XBA	VB16N,
	SSR	0	16,
TXFE=	ASP	0/B,
	KE	FEHL3,
	XBA	VB16E,
	SSR	0	16,

TXTTRANS=	R	MCF	A	-- TRANSPORT VON STANDARDTEXTEN IN DRUCKPUFFER --
	B2V	0		: IN A ANFANGSADRESSE TEXT ;
	SH	A	16	: ANZAHL GW DES TEXTES ALS 1. OKTADE IM TEXT ;
	SU	ALWGT,
	SH	AL	1	-- IN A STEHT ANZAHL GW --
	RX	AN	XPP,
	SH	AK	8,
	MAB	AUT	0	-- IM 1. TEILWORT 1. OKTADE AUSBLENDEN --
	MAB	C	0,
	MU	S	0,

TXDRSTI=	BZ	('' STARTSATZ:'') 	-- UP ZUM DRUCK DES STARTSATZES --
	E	CZ	XPP,
	HXP	4	XPP,
	SU	ALDRUCK,
	B2	TXSTI+1		-- KOPFWORT ABWICKLER --
	SH	A	16	-- ANZAHL GW --
	TRX	AN	XA,
	XBA	TXSTI,
	VBA	2,
	XC	XB,
TXDR1=	B	('15AFAF AFAFAF'/3),
	E	C	XPP,
	HXP	2	XPP,
	EZ	BU	XB,
	SU	ALWOWA,
	SU	ALDRUCK,
	SZX	TXDR1	YA,
	MU	S	0,

TXTFEHL=	XB	XPP		-- AUSDRUCKEN PUFFER, DRUCK FEHLERTEXT --
	VBA	S&DPUF,
	SXI	2R,
	SU	ALDRUCK,
	XBA	TXTF0,
	BA	4,
	SU	ALWGT,
	MU	S	0,

TXDUMP=	XBA	S&DPUF,
	XC	XPP,
	XBA	TXDUM,
	BA	5,
	SU	ALWGT,
	SU	ALDRUCK,
	XBA	VB60,
	SSR	6	0,
	MU	S	0,

TXAL1=	CZ	ALV2,
	SK0	ALF1		: ALARMART NEGATIV ;
	CQ	ALV2,
	AA	1,
	BAR	3,
	SK	ALF1		-- ALARMART ZU GROSS --
	RLR	S	H,
	S	AL0,
	S	AL1,
	S	AL2,
AL0=	B	ALV2+2		: EREIGNISALARM, RQ AUSWERTEN ;
	SBIT	BIT1	1A,
	SBIT	BIT2	2A,
	SBIT	BIT3	3A,
	SBIT	BIT4	4A,
	SBIT	BIT5	5A,
	SBIT	BIT6	6A,
	SBIT	BIT7	7A,
	SBIT	BIT8	8A,
	SBIT	BIT9	9A,
	SBIT	BIT10	10A,
	SBIT	BIT11	11A,
	SBIT	BIT12	12A,
	SBIT	BIT13	13A,
	SBIT	BIT14	14A,
	SBIT	BIT15	15A,
	SBIT	BIT16	16A,
	S	AL02,

BIT1=	XBA	TEROP,
	ET	('7FFFFF FFFFFF'),
	S	AL01,
BIT2=	XBA	TERFA,
	ET	('BFFFFF FFFFFF'),
	S	AL01,
BIT3=	XBA	TERZEI,
	ET	('DFFFFF FFFFFF'),
	S	AL01,
BIT4=	XBA	TERSEIT,
	ET	('EFFFFF FFFFFF'),
	S	AL01,
BIT5=	XBA	TERHALT,
	ET	('F7FFFF FFFFFF'),
	S	AL01,
BIT6=	XBA	TERENT,
	ET	('FBFFFF FFFFFF'),
	S	AL01,
BIT7=	XBA	TERGGEB,
	ET	('FDFFFF FFFFFF'),
	S	AL01,
BIT8=	XBA	TEROZEIT,
	ET	('FEFFFF FFFFFF'),
	S	AL01,
BIT9=	XBA	TERVER,
	ET	('FF7FFF FFFFFF'),
	S	AL01,
BIT10=	XBA	TERBOT,
	ET	('FFBFFF FFFFFF'),
	S	AL01,
BIT11=	XBA	TERABA,
	ET	('FFDFFF FFFFFF'),
	S	AL01,
BIT12=	XBA	TERBEE,
	ET	('FFEFFF FFFFFF'),
	S	AL01,
BIT13=	XBA	TERUMM,
	ET	('FFF7FF FFFFFF'),
	S	AL01,
BIT14=	XBA	TERSSR,
	ET	('FFFBFF FFFFFF'),
	S	AL01,
BIT15=	XBA	TERNRM,
	ET	('FFFDFF FFFFFF'),
	S	AL01,
BIT16=	XBA	TERFHL,
	ET	('FFFEFF FFFFFF'),
	S	AL01,

AL01=	C	ALV2+2,
	BA	7,
	SU	ALWGT,
	S	AL03,
AL02=	XBA	TER		-- UNDEF. EREIGNISALARM --
	BA	3,
	SU	ALWGT,
	B	TERIN,
	E	C	XPP,
	HXP	2	XPP,
	B	ALV2,
	LC	ALV2+2,
	SU	ALWOWA,
AL03=	SU	ALDRUCK,
	B	ALV2+2,
	SI0	AL3,
	XBA	TBLNK,
	BA	2,
	SU	ALWGT,
	S	AL0,
AL1=	B	ALV3+26		-- HARDWARE-ALARM --
	STN	3	2A,
	XBA	THMAK,
	S	AL11,
	LR	3Q,
	BH	ALV3+24,
	ETA	'F88',
	SH	Z	7,
	SH	Q	38	-- BEED VON 4 NACH 42 --
	R	VEL	Q,
	RT	AQ,
	B3	ALV3+26,
	RT	AH,
	ETA	'80',
	SH	A	2,
	R	VEL	Q,
				-- ANSTEUERUNG DES TEXTES ENTSPRECHEND ALARMBIT --
	SI0	AL12		-- KEIN ALARM MIT VORHANDENEM FEHLERTEXT STEHT AN --
	BAR	'8'		-- REBUE --
	SN	AL10,
	QBR	ALV3+16		-- REGISTERINHALTE BEIM ALARMSPRUNG --
	ZU	XU1		-- EINSTELLEN DES DURCH QBR ZERTSOERTEN U-REG. --
	STN	ALA1	0A,
	SN0	AA1,
	STN	AAF	0Q,
	STN	AAF	0D,
	RT	AQ,
	SN0	AAF,
	RT	AD,
	SI0	AAF,
	BA	THREB1,
	XBA	5,
	S	AAE		-- GLEITKOMMADIVISION DURCH 0 --
AA1=	SH	A	7,
	SRN	AA3		-- EXPONENT WAR POSITIV --
	STN	AA4	0Q,
	STN	AAF	0D,
	RT	AQ,
	SN0	AAF,
	RT	AD,
	SN0	AA2,
	BA	THREB2,
	XBA	5,
	S	AAE		-- UEBERLAUF GLEITKOMMADIVISION --
AA2=	BA	THREB3,
	XBA	6,
	S	AAE		-- UEBERLAUF GLEITKOMMAARITHMETIK --
AA3=	RT	AQ,
	R	A	D,
	SAA	AAF,
	STN	AAF	1A,
	SN0	AAF,
	B	THREB4,
	XBA	5,
	S	AAE		-- UEBERLAUF FESTKOMMADIVISON --
AA4=	STN	AAF	1Q,
	RT	AD,
	R	A	H,
	SAA	AAF,
	SN0	AAF,
	STN	AAF	1A,
	BA	THREB5,
	XBA	8,	S	AAE		-- UEBERLAUF DOPPELTL. GLEITKOMMAARITHMETIK --
ALA1=	ST	AAF	1A,
	STN	AAF	1D,
	C	ALV5,
	SAA	AA6		-- A1 UNGLEICH A2 --
	SI0	AA5,
	SK0	3R,
	NRM	N,
	S	2R,
	NRM	L,
	BA	25,
	R	BH	Y,
	SN	AAF,
	RT	AD,
	SN0	AAF,
	B	THREB6,
	XBA	7,
	S	AAE		-- UEBERLAUF FESTKOMMA-HALBWORTARITHMETIK --
AA5=	RT	AD,
	R	A	Q,
	SAA	AAF,
	STN	AAF	1A,
	SI0	AAF,
	BA	THREB7,
	XBA	5,
	S	AAE		-- FESTKOMMADIVISON DURCH 0 --
AA6=	BA	0,
	RT	AD,
	SI0	AA7,
	BA	THREB8,
	XBA	5,
	S	AAE		-- UEBERLAUF FESTKOMMAARITHMETIK --
AA7=	RT	AQ,
	SN0	AAF,
	STN	AAF	1A,
	BA	THREB9,
	XBA	10		-- REGISTEROPERAND BEI HALBWORTMULT. IST ZU GROSS --
AAE=	C2	ALV5+1,
	TBC	ALV5,
	XBA	THREBUE,
	BA	3,
	SU	ALWGT,
	XBA	THREBA,
	BA	6,
	SU	ALWGT,
	TCB	ALV5+1,
	B2	ALV5,
	S	AL21,
AAF=	XBA	THREBUE,
	S	AL11,
AL10=	BA	'10'		-- REAL --
	SI	(XBA THREAL, S AL11),
	BA	'4'		-- BEEC --
	SI	(XBA THBEEC, S AL11),
	BA	'1'		-- BEEK --
	SI	(XBA THBEEK, S AL11),
	BA	'2'		-- BEEU --
	SI	(XBA THBEEU, S AL11),
	BA	'24'		-- BEIC --
	SI	(XBA THBEIC, S AL11),
	BA	'40'		-- BEED --
	SN	AL12		-- MEHRERE ALARMBITS SIND GESETZT --
	XBA	THBEED,
AL11=	BA	3,
	SU	ALWGT,
	B	(''ALARM.''),
	E	C	XPP,
	HXP	2	XPP,
	S	AL13,
AL12=	XBA	THARD,
	S	AL11,
AL13=	SU	ALDRUCK,
	S	AL3,
AL2=	XBA	TAL2		-- ALARMART 2 --
	BA	10,
AL21=	SU	ALWGT,
	SU	ALDRUCK,
AL3=	TCB	TXSTI+5,
	MU	SXN	0	-- KEIN AUSDRUCK DES ALARMKELLERS GEWUENSCHT --
	XBA	THKEL		-- AUSDRUCK ALARMKELLER --
	BA	7,
	SU	ALWGT,
	SU	ALDRUCK,
	XBA	THK1,
	BA	11,
	SU	ALWGT,
	SU	ALDRUCK,
	XBA	ALV3+4,
	SU	ALKDR,
	XBA	THK2,
	BA	11,
	SU	ALWGT,
	SU	ALDRUCK,
	XBA	ALV3+12,
	SU	ALKDR,
	XBA	THK3,
	BA	11,
	SU	ALWGT,
	SU	ALDRUCK,
	B3	TXSTI,
	C3	VB40A+1,
	XBA	VB40A,
	SSR	4	0,
	B	THBEEK,
	C	S&DPUF+18,
	BU	ALV3+22,
	SU	ALWOWA,
	BU	ALV3+24,
	SU	ALWOWA,
	BU	ALV3+26,
	SU	ALWOWA,
	XB	XPP,
	MAB	B	-6,
	VEL	THLE,
	MAB	C	0,
	MAB	B	-6,
	VEL	THLE,
	MAB	C	0,
	MAB	B	-6,
	VEL	THNZL,
	MAB	C	0,
	HXP	6	XPP,
	SU	ALDRUCK,
	XBA	NL,
	BA	1,
	SU	ALWGT,
	SU	ALDRUCK,
	MU	S	0,
ALF1=	XBA	TFAAL		-- FALSCHER ALARMSCHLUESSEL--
	BA	4,
	SU	ALWGT,
	B	ALV2,
	SU	ALWOWA,
	B	ALV2+2,
	SU	ALWOWA,
	XB	XPP,
	MAB	B	-6,
	VEL	THLE,
	MAB	C	0,
	SU	ALDRUCK,
	MU	S	0,

ALWGT=	XC	XR		: UP ZUM TRANSPORT IN AUSG.-PUFFER ;
	R	TCB	A,
	MU	SXKG	0,
	WTV	XPP	XR,
	HBA	2,
	XC	XPP,
	MU	S	0,

ALWOWA=	EZ	LC	XPP	-- UMWANDLUNG GANZWORT IN ZENTRALCODE --
	EZ	LC	XPP,
	XC	XR,
	RT	AQ,
	ST	4	2Q,
	ST	4	1Q,
	ST	4	0Q,
	MA	'100',
	MA	'100',
	MA	'100',
	BA	'B0AF',
	ZTR	3Q,
	HXP	-2	XPP,
	ZTR	3A,
	MAB	C	-2,
ALW1=	ZX	-5	XL,
ALW2=	LR	3A,
	SH	ZL	4,
	BAR	9,
	SGG	2R,
	MA	'06',
	BA	'B0',
	R	A	H,
	RT	AH,
	E	B	XPP,
	SH	AL	8,
	R	VEL	H,
	E	C	XPP,
	SZX	ALW2	XL,
	HXP	2	XPP,
	HBPX	-1	XR,
	SXKG	ALW1,
	MU	S	0,

ALDRUCK=	XBA	S&DPUF		-- DRUCK DES AUSGABEPUFFERS --
	XC	XANF,
	SLN	MA2	2	-- KEINE KONSOLAUSGABE --
	XC	XZR,
MA1=	XBA	'8000',
	XC	XZL,
	BH	('AF'/3) 	-- AUFBRUCH-KRITERIUM --
	ZX	0	XZA	-- ZEICHENZAEHLER --
	XC	XGW,
MA3=	BNZ	XZL	XZR,
	SN	3R,
	TXX	XGW	XZR	-- ADRESSE --
	TXX	XZNR	XZL	-- ZEICHEN-NR. --
	XB	XPP,
	HBPX	-1	XZR,
	SXKG	MA2		-- TEXTENDE --
	SI0	4R		-- IGNORE --
	HXP	1	XZA,
	RX	BN	XZBR,
	SXK	2R,
	S	MA3,
	SXN	3R,
	TXX	XGW	XZR	-- KEIN ' ' IM TEXT --
	TXX	XZNR	XZL,
	E	BQ	XGW,
	TXR	A	XZNR,
	LA	F		-- ZEICHENNR. +1 --
	SI0	MA5,
	SBA	1,
	TRX	AN	XZ1,
	LR	3A,
MA4=	SH	ZUL	8,
	SZX	MA4	XZ1,
MA5=	E	C	XGW	-- ZEILENENDE --
	CQ	HISP		-- ANFANG NEUE ZEILE --
	XB	XANF		-- VERSORGUNGSBLOCK --
	TBC	VB+2,		-- AA --
	XB	XGW,
	HBPX	-1	XANF,
	HBA	2,
	SHB	R	1,
	TBC	VB+3		-- LNG --
	XBA	VB,
	SSR	6	16	-- DRUCK --
	B	HISP,
	ENZ	C	XGW	-- ANF. NEUE ZEILE --
	XC	XANF		-- AA NEUE ZEILE --
	XC	XZR,
	B	NL		-- ZEILENVORSCHUB --
	MAB	C	0,
	S	MA1,
MA2=	XB	XANF		-- KEINE KONSOLAUSGABE BZW. PUFFERENDE --
	TBC	VB+2,
	XB	XPP,
	HBPX	-1	XANF,
	SHB	R	1,
	TBC	VB+3,
	XBA	VB,
	SSR	6	16,
	XBA	S&DPUF,
	XC	XPP,
	MU	S	0,

ALKDR=	XC	XA		-- SPEICHERT 1 ZEILE ALARMKELLER UND DRUCKT --
	ZX	-3	XB,
	EZ	BU	XA,
	SU	ALWOWA,
	SZX	-2	XB,
	BH	THLE,
	ZX	-2	XB,
	XB	XPP,
	MAB	B	-6,
	R	VEL	H,
	MAB	C	0,
	SZX	-3	XB,
	MAB	B	-6,
	VEL	THNZL,
	MAB	C	0,
	SU	ALDRUCK,
	MU	S	0,

TXTDM=	TDM	LISTUE/K,
	TDM	LISTF/K,
	TDM	LISTA/K,
	TDM	LISTC/K,
	UVB	1,
	S	TXF2/K,
	TDM	LISTB/K,
	S	TXF2/K,
	S	TXF2/K,
	TDM	LISTP/K,
	UVB	0,

S&DPUF=	CZONE	V,
	ABLAGE	S&DPUF	(V0),
S&DPUF=	ASP	44/G,
PUFEND=	ASP	2,
	AEND	(V0),

XBR=	ZONE	I,
	ABLAGE	XBR	(V0),
TXXB=	ASP	18/G		-- INDEXSPEICHER --
	AEND	(V0),

TXSTI=	ASP	34/G		-- STEUERINFORMATION --
ALV2=	ASP	4/G		-- ALARMINFORMATION BEI ANSPRUNG --
ALV3=	ASP	28/G		-- ALARMKELLER --
ALV5=	ASP	2/G		-- ZWISCHEN SPEICHER --
HISP=	0/3V,
TXSTKE=	ASP	26/G		-- KENNDATEN --
VB40A=	S&SRF/AGV, '0'/HV, S&DPUF+16/AV, '0'/HV,
VB=	TXFENDN/AGV, '3'/HV, '0'/2V, '0'/V,
VB14=	S&SRF/AGV, 0/HV, TXSTI/AV, '11'/VH,
VB616=	TXFENDN/AVG, '3'/HV, S&DPUF/AV, '0'/HV, '0'/V,
VB48=	S&SRF/AGV, '0'/HV, ALV3/AV, '0'/HV,
VB022=	S&SRF/AG, '1'/H,
VB18=	S&SRF/AG, '0'/H, '0'/3,
VB40=	S&SRF/AG, '0'/H, TXSTKE/A, '0'/H,
VB16=	TXFF/AG, '0'/H, '1',
VB16N=	S&SRF/AG, '0'/H, '2',
VB16E=	S&SRF/AG, '0'/H, '3',
VB60=	S&SRF/AG, '0'/H, TXXB/A, VB48+2/A,
VBZUSL=	S&SRF/AG, '0'/H,
VBZUSS=	S&SRF/AG, '1'/H,
TFEHL=	''*017FEHLER: '',
TWARN=	''*017WARNG.: '',
TBLNK=	''*021        '',
TER=	''EREIGNIS-ALARM: '',
TEROP=	''OPERATEURALARM                       '',
TERFA=	''SSR-FEHLERAUSGAENGE 1024 MAL ANGESPRUNGEN'',
TERZEI=	''RECHENZEITSCHRANKE UEBERSCHRITTEN    '',
TERSEIT=	''SEITENUEBERLAUF DES ABLAUFPROTOKOLLS '',
TERHALT=	''HALT-ANWEISUNG VOM TERMINAL          '',
TERGGEB=	''GEMEINSCHAFTSGEBIET ZU LANGE GESPERRT'',
TEROZEIT=	''OPERATORLAUF-ZEITSCHRANKE UEBERSCHRITTEN'',
TERVER=	''VERDRAENGUNGSBOTSCHAFT EINGETROFFEN       '',
TERBOT=	''ABBRUCHBOTSCHAFT EINGETROFFEN             '',
TERABA=	''ABBRUCH DURCH BOTSCHAFT (XEN, ABBR, ABBRO)'',
TERBEE=	''BEENDE-ANWEISUNG VOM TERMINAL              '',
TERUMM=	''ALARMADRESSENUMMELDUNG BEI UMMELDESPERRE'',
TERSSR=	''SSR-FEHLER BEI UMLENKUNG                '',
TERNRM=	''ABBRUCH DURCH NORMALENDE                '',
TERFHL=	''ABBRUCH DURCH FEHLERENDE                '',
TERENT=	''ENTSCHLUESSLER-GRUNDSTUFE GEWUENSCHT    '',
TERIN=	'' RQ = '',
THREB1=	''GLEITKOMMADIVISION DURCH 0.'',
THREB2=	''UEBERLAUF GLEITKOMMADIVISION.'',
THREB3=	''UEBERLAUF GLEITKOMMA-ARITHMETIK.'',
THREB4=	''UEBERLAUF FESTKOMMADIVISION.'',
THREB5=	''UEBERLAUF DOPPELTLANGE GLEITKOMMA-ARITHMETIK.'',
THREB6=	''UEBERLAUF FESTKOMMA-HALBWORT-ARITHMETIK.'',
THREB7=	''FESTKOMMADIVISION DURCH 0 '',
THREB8=	''UEBERLAUF FESTKOMMA-ARITHMETIK.'',
THREB9=	''REGISTEROPERAND BEI HALBWORT-MULTIPLIKATION IST ZU GROSS.'',
THREBA=	''ALARM. MOEGLICHE FEHLERSITUATION: '',
THMAK=	''           MAKRO-'',
THREBUE=	''ARITHMETISCHER '',
THREAL=	''TYPENKENNUNGS-'',
THBEEC=	''SPEICHERSCHUTZ-'',
THBEEK=	''       BEFEHLS-'',
THBEEU=	''UEBERLAUF U-REG.-'',
THBEIC=	''          BEIC-'',
THBEED=	''DREIERPROBEN-'',
THARD=	''               '',
THKEL=	''*019                              ALARMKELLER'',
THK1=	''*020         K Y U            A                 Q                 D'',
THK2=	''*020        H            BT    0           X     0           BLZ   RS'',
THK3=	''*020   B     BA          F     STB1        BCSTB2  BH        OLN'',
THNZL=	''*021 '',
THLE=	''    '',
TAL2=	''VERSORGUNGSBLOCK EINES SSR-BEFEHLS IST NICHT AUSWERTBAR.'',
TFAAL=	''FALSCHE ALARMART. A,Q = '',
TXTF0=	''*021FEHLER: TEXTOPERATOR, '',
TXTF1=	''FALSCHER ARBEITSSCHLUESSEL AS.'',
TXTF2=	''FALSCHER SPRACHSCHLUESSEL SS. '',
TXTF3=	''KEIN FEHLERTEXT VORHANDEN.'',
TXDUM=	''*017VARIABLENDUMP TEXTOPERATOR:'',

				--- ADRESSENLISTE FEHLERTEXTE UEBERSPRACHLICHE PROZEDUREN --
LISTUE=	0,0,0,0,0,0		-- FEHLT --

				--- ADRESSENLISTE FEHLERTEXTE FORTRAN-BIBLIOTHEKSPROZEDUREN --
LISTF=	0,0,0,0,0,0,0,0		-- FEHLT --

				--- ADRESSENLISTE FEHLERTEXTE ALGOL-BIBLIOTHEKSPROZEDUREN --
LISTA=	0,0,0,0,0,0		-- FEHLT --

				--- ADRESSENLISTE FEHLERTEXTE COBOL-BIBLIOTHEKSPROZEDUREN --
LISTC=	0,0,0,0,0,0,0,0		-- FEHLT --

				--- ADRESSENLISTE FEHLERTEXTE BASIC-BIBLIOTHEKSPROZEDUREN --
LISTB=	0,0,0,0,0,0,0,0		-- FEHLT --

				--- ADRESSENLISTE FEHLERTEXTE PL1-BIBLIOTHEKSPROZEDUREN --
LISTP=	0,0,0,0,0,0,0,0		-- FEHLT --

				--- FEHLERTEXTE --

T1=	''*004ARITHMETISCHER ALARM.'',
T2=	''*009ZEIN, ANZAHL DER SUBROUTINE-PARAMETER IST FALSCH'',
T3=	''*006ZEIN, EIN PARAMETER HAT FALSCHE ART'',
T4=	''*007ZEIN, PARAMETER IST NICHT VOM TYP INTEGER'',
T5=	''*006ZEIN, CODE-SCHLUESSEL IST FALSCH'',
T6=	''*008ZEIN, CODE-SCHLUESSEL IST KEIN INTEGER-INHALT'',
T7=	''*007UEBERLAUF BEI DOPPELTLANGER DIVISION'',
T8=	''*006DOPPELT LANGE DIVISION DURCH 0'',

	ENDE,
