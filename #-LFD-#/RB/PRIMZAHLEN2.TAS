
PRIMZAHLEN2.=	SEGM,
	DRUCK	3,

	EXTERN	A&FSP,
	EXTERN	S&WKERN,

	INDEX	20	(XI,XK,XPRIME,XFLAGS,XZAEHL,XANZAHL,XW1,XW2),

		 		-- HIER BEGINNT DAS PROGRAMM --

	R&DRUCKE,

	DRUCK	2,

	XBA	VB616,
	SSR	6	16	-- STARTMELDUNG --

	B	(1000000) 		-- MAXIMALE ZU BERECHNENDE PRIMZAHL --
	SH	A	1,
	C2	ANZBITS		-- HALBE MAXIMALE PRIMZAHL --

	RT	AQ,
	LR	1A,
	DVD	(24)		-- 24 BITS PRO HALBWORT --
	AA	3,
	SH	A	1,
	C2	ANZGW		-- ANZAHL DAFüR BENöTIGTER GANZWORTE --


	BAR	1,
	SFB	A&FSP		-- IM FREISPEICHER BESORGEN --
	TRX	A	XFLAGS	-- ANF.ADR. FLAGSPEICHER --

INIT=	B2V	ANZBITS,
	SH	AL	1,
	LR	1Q,
	NRM	FG,
	GML	(7.03687441776641E13) 	 -- 2 ** 46 --
	SU	S&WKERN,
	AU	(9/0),
	SH	AR	10,
	ZTR	1A,
	AA	6,
	C2	WURZEL		--	WURZEL := INTEGER(SQRT(FLOAT(SIZE*2))) + 6 ; --

	SU	VORLOESCHEN 	-- ALLE FLAGS VORBESETZEN MIT TRUE --

LOS=	XBA	0,
	XC	XI,
SCHL1=	E	BA	XI	-- FOR I := 0 TO WURZEL DO			--
	SU	UPGET		-- 	IF FLAGS [I] THEN			--
	SI0	NOT1,

	XB	XI		-- 		PRIME := I * 2 + 3 ;		--
	SHB	L	1,
	HBA	3,
	XC	XPRIME,

	RX	B	XI	-- 		K := I + PRIME ;		--
	XC	XK,

	B2V	ANZBITS,
	RT	AH,
SCHL2=	R	B2V	B	--		WHILE K <= SIZE DO		--
	SG	ESCHL2,
	SU	UPLOESCH 		--			FLAGS [K] := FALSE ;	--
	HXX	XPRIME	XK	--			K := K + PRIME ;	--
	S	SCHL2,
ESCHL2=	ASP	0/B		--		END ;				--	

NOT1=	ASP	0/B		--	END ;					--

	HXP	1	XI,
	VBC	WURZEL,
	SXKG	SCHL1		-- END ;					--

	BA	1000,
	C	VONNICHT,
	B2	ANZBITS,
	ZTR	1A,
	SH	AL	1,
	SBA	1000,
	C	BISNICHT 		-- VON 1000 .. N-1000 NICHT PROTOKOLLIEREN --

	LZL	0	1	-- VOR LUECKE --

PRINT=	ZX	0	XANZAHL	-- ANZAHL := 0 ;				--
	ZX	-9	XZAEHL,
	ZX	0	XI	-- FOR I := 0 TO SIZE DO			--
SCHL4=	E	BA	XI	-- 	IF FLAGS [I] THEN			--
	SU	UPGET,
	SI0	ESCHL4,
	HXP	1	XANZAHL	--		ANZAHL := ANZAHL + 1 ;		--

	TXR	A	XI	--		PRINT (I*2+3) ;		--
	SH	AL	1,
	AA	3,

	BH	VONNICHT,
	SK	PRINT3		-- VOR DER NICHT-AUSGABE-LüCKE --

	SL	PRINT2	1,
	LZL	1	0	-- HIER 1.MAL IN DER LüCKE --

	B	(''   ...''),
	R&AOKT	(6),
	S	PRINT4,

PRINT2=	BH	BISNICHT,
	SK	ESCHL4		-- IN DER NICHT-AUSGABE-LüCKE --

PRINT3=	ASP	0/B		-- HIER WIRD AUSGEGEBEN	--

	R&AGANZ	(8),
	SZX	INPUFFER XZAEHL,
PRINT4=	ZX	-9	XZAEHL,
	R&DRUCKE 			--		ALLE 10 ZAHLEN NEUE ZEILE	--
INPUFFER=	ASP	0/B,
ESCHL4=	HXP	1	XI,
	VBC	ANZBITS,
	SXK	SCHL4		-- END ;					--

	R&DRUCKE,
	R&DRUCKE,

	TXR	A	XANZAHL,
	R&AGANZ	(8),

	B	('' Primz''),
	R&AOKT	(6),
	B	(''ahlen ''),
	R&AOKT	(6),
	B	(''gesamt''),
	R&AOKT	(6),
	B	('' unter''),
	R&AOKT	(6),

	B2V	ANZBITS		-- HALBE MAXIMALE PRIMZAHL --
	SH	AL	1,
	R&AGANZ	(9),

	R&DRUCKE,

				--	KB	'4711'	--

	R&ENDE,


UPSETZ=	SU	BITADR,
	R	VEL	Q	-- OR MASKE --
	MAB	C2	0	-- UND SPEICHERN --
	MU	S	0,

UPLOESCH=	SU	BITADR,
	IR	Q,
	R	ET	Q	-- AND NOT MASKE --
	MAB	C2	0	-- UND SPEICHERN --
	MU	S	0,

UPGET=	SU	BITADR,
	R	ET	Q	-- AND MASKE --
	MU	S	0,

BITADR=	ASP	0/B		-- IN RA IST BITNUMMER, RH BLEIBT ERHALTEN --
	RT	AQ,
	LR	1A,
	DVD	(24),
	RX	A	XFLAGS	-- B = >HALBWORT< --
	BA	1,
	R	MCFU	Q	-- MIT DIVISIONSREST DAS BIT IM HALBWORT SHIFTEN --
	SH	AL	0,
	RT	AQ		-- Q = BITMASKE IM HALBWORT --
	MAB	B2	0	-- A = HALBWORT MIT BIT DRIN --
	MU	S	0,

VORLOESCHEN=	ASP	0/B,
	XB	XFLAGS,
	XC	XW2,
	LR	3A,
	IR	A,
	MAB	C	0	-- 1. WORT MIT 'FFFFFF FFFFFF' LöSCHEN --
	HBA	2,
	XC	XW1,
	TCB	ANZGW		-- ANZAHL GANZWORTE --
	VBA	1		--  -1	--
	WTV	XW1	XW2	-- ALLE LöSCHEN --
	MU	S	0,

	R&ENDE,

ANZBITS=	0/HV1			-- HALBE MAXIMALE ZU BERECHNENDE PRIMZAHL --
ANZGW=	0/HV1,
WURZEL=	0/HV1,
VONNICHT=	0/V,
BISNICHT=	0/V,


VB616=	R&SSRFEHLER/AG, 3/H,
	TXT/AG, 10/H,

TXT=	''*020Primzahlen (Index-Arithm.) mit Sieb des Eratosthenes :*020'', 0/2,

	ENDE,
