-- PS&TASAOP			KLASSE 1  KC 1	KUHLMEY	CGK/TP4	--
-- --
-- --

PS&TASAOP=	SEGM,
	START	TA1,
	ALARM	S&ALAD,
	XBASIS	XB,
	UNTPR	30,
	VORBES	(3,'000000000000'),
	EXTERN	S&ALARM	(S&ALARM,S&ALAD),
	EXTERN	S&SRF	(S&SRF,S&SRFR),
	EXTERN	S&GZF	(S&GZF,S&GZFA,S&GZFE),
	EXTERN	S&OPZEIT,
	EXTERN	S&SMFEHL,
	INDEX	8	(X0,X1,CNL,CNR,X3,GWX,ZJK,ZPF,XVG),

TA1=	B2	(TAF3/A),
	SU	S&ALARM,
	B2	(TAF1A=	TAF1/A),
	SU	S&SRF+1,
	B2	TAF1A,
	SU	S&SMFEHL+1,
	LZL	0	'FF',
	XBA	VB14,
	SSR	1	4,
	KE	INIT,
	B3	STI+5,
	BAR	4,
	SG	TA1F,
	BA	7,
	SG	MODUS		-- TAS --
TA1F=	XBA	1,
	S	TF1		-- UNBEKANNTE SPRACHE --
MODUS=	TCB	STI+8		-- P=6: MODUS --
	SXI	PUF,
	R	B3	B,
	SHB	R	16,
	VBA	1,
	SXI	PUF		-- STD --
	VBA	4,
	SXN	TA31		-- UNZULAESSIGER PARAMETER BEI SPEZ. MODUS --
	SH	AL	4,
	R	B3V	A,
	SH	A	4,
	R	MCFU	A,
	B2	STI,
	SH	ZU	8,
	BAR	''MK'',
	SI	(LZL 1 0, S PUF)	--KOMPRESSION MIT KOMMENTAREN --
	BA	''EN'',
	SI	(LZL 4 0, S PUF)	--ENTZERRUNG --
	BA	''OK'',
	SI	(LZL 2 0, S PUF)	--KOMPRESSION OHNE KOMMENTARE--
TA31=	XBA	2,
	S	TF1		--UNZULAESSIGER PARAMETER BEI SPEZ. MODUS --
PUF=	TCB	STI+3		--P=1: QUELLE --
	R	B3	B,
	SHB	R	16,
	VBA	2,
	SXI	TA20		--QUELLE BEFINDET SICH IN EINER DATEI--
	VBA	4,
	SXI	TA21		--QUELLE BEFINDET SICH IM STARTSATZ--
	AA	STI		--QUELLE BEFINDET SICH IN EINEM GEBIET--
	R	MCFU	A,
	BZ	0		--GEBIETSINFORMATION--
	CZ	VBGZFG+8,
	BA	VBGZFG,
	S	TA22,
TA20=	SU	DATPAR,
	CH	VBGZFD+8,
	C	VBGZFD+10,
	CQ	VBGZFD+14,
	TCB	STI+4		--P=2: BEREICH--
	SXI	TA202		--KEINE ANGABE: QUELLE SOLL GANZ GELSEN WERDEN--
	R	B3	B,
	SHB	R	16,
	VBA	1,
	SXI	TA202		-- STD --
	SBA	'2000',
	AA	STI,
	R	MCF	A,
	B2V	1		-- ANFANGSNUMMER --
	RT	AQ,
	KDFR	6,
	SBA	1,
	SKG0	TA201,
	C	VBGZFD+12,
	ZT3	VBGZFD+4,
	BA	1,
	C2	VBGZFD+5		-- SRB=1 --
TA201=	MAB	B2V	3	-- ENDNUMMER --
	SI0	TA202,
	RT	AQ,
	KDFR	6,
	R	BH	A,
	ZTR	3A,
	CR	ESNR,
	SB	VBGZFD+12,
	SAA	TA201F,
	SG0	TA202,
TA201F=	XBA	3,
	S	TF1		--UNZULAESSIGER PARAMETER BEI SPEZ. BEREICH--
TA202=	BA	VBGZFD,
	S	TA22,
TA21=	AA	STI		--OKTADENSTRING--
	C2	VBGZFP+5,
	BA	VBGZFP,
TA22=	SFB	S&GZFA,
	N	0,
TA6=	TCB	STI+7		-- P=5: NUMERIERUNG--
	SXI	TA7		--STANDARDNUMERIERUNG--
	R	BH	B,
	SHB	R	16,
	VBA	1,
	SXI	TA7		-- STD --
	ETA	'FFF',
	AA	STI,
	R	MCF	A,
	B2	0,
	RT	AQ,
	BA	0,
	ZTR	1Q,
	KDFR	6,
	C	SNR,
	MAB	B2	1,
	RT	AQ,
	BA	0,
	ZTR	1Q,
	KDFR	6,
	C	SW		-- SCHRITTWEITE --
TA7=	B3	STI+6		--P=4: ZIEL--
	SU	DATPAR,
	CH	VBTDAT+2,
	C	VBTDAT+4,
	CQ	VBTDAT+12,
	R	TCB	A	--ANZAHL GW DES DATEINAMENS--
	CH	VBTD2+2,
	VBA	1,
	CQ	VBTD2+12,
	ZTR	2A,
	XCN	X3,
	C	VBTD2+4,
	VBC	VBGZFD+11,
	HBA	1,
	SXN	ANZVOR		--DATEINAMEN EINGABEDATEI /TDATEI UNGLEICH LANG--
	R	TCB	AL	-- >DATEINAME TDATEI< --
	VBA	2,
	XC	X1,
	TCB	VBGZFD+10		-- >DATEINAME EINGABEDATEI< --
	SXI	ANZVOR		-- QUELLE LIEGT NICHT IN EINER DATEI--
	VBA	2,
	XC	X0,
	B	VBGZFD+8,
	SN	ANZVOR		--DATENBASEN SIND NICHT IDENTISCH --
TA71=	EZ	B	X1,
	EZ	BH	X0,
	SN	ANZVOR,
	SZX	TA71	X3,
	XBA	4		--EINGABEDATEI IDENTISCH MIT ZIEL --
	S	TF1,
ANZVOR=	XBA	VBINT		-- VORBEREITUNG   ANZ --
	SSR	253	3	-- KREATION     QUELLE--
	BA	7,
	XBA	VBINT,
	C	VBINT+6		-- BA SCHREIBEN --
	SSR	253	9,
	LC	VBINT+6,
	BA	1,
	XBA	VBINT,
	C2	VBINT+11,
	SSR	253	11	-- POSITIONIEREN AUF SATZ 1 --
	RT	AD,
	SI0	2R,
	S	F11,
	XBA	13,
	BA	NZP,
	TBC	VBINT+9,
	C2	VBINT+8,
	XBA	NZP,
	XC	CNR,
	XBA	'8000',
	XC	CNL,
	S	E21,
E1=	SFB	ENZ,
E2=	BA	'AF',
	SN	E22,
E20=	SFB	ANZ,
E21=	SFB	ENZ,
	BA	'AF',
	SI	E21		-- LEERSTELLEN ELIMINIEREN --
E22=	BA	'61'		-- ' --
	SI	E3,
	BA	'91'		-- - --
	SI	E5,
	BA	'78'		-- * --
	SI	E4,
	BA	'AA'		-- , --
	SI	E52,
	BA	'AB'		-- : --
	SN	E24,
E23=	SL	2	2,
	SFB	ANZ,
	SFB	ENZ,
	BA	'AC'		-- ; --
	SN	E23,
	SLN	E52	2,
	S	E510,
E24=	SFB	ANZ,
	S	E1,
E3=	CH	VGL		-- ABSPEICHERN STRING ODER KOMMENTAR UNVERAENDERT --
	SFB	ANZ,
	SFB	ENZ,
	B	VGL,
	SN	E2,
E31=	SFB	ANZ,
	SFB	ENZ,
	B	VGL,
	SN	E31,
	SFB	ANZ,
	SFB	ENZ,
	B	VGL,
	SN	E31,
	BA	'91'		-- - --
	SI	E52,
	S	E20,
E4=	SFB	ANZ		--ÜBERTRAGEN FOLGE *( UNVERAENDERTER KLAMMERINHALT --
	LC	VGL,
	SFB	ENZ,
	BA	'AF',
	SN	E41,
	SFB	ANZ,
E40=	SFB	ENZ,
	BA	'AF',
	SI	E40,
E41=	BA	'A0'		-- ( --
	SN	E22,
	BA	1,
	AC	VGL,
	SFB	ANZ,
E42=	SFB	ENZ,
	BA	'A0',
	SI	(BA 1, AC VGL, S E43),
	BA	'A1',
	SN	E43,
	BA	1,
	SBC	VGL,
E43=	SFB	ANZ,
	B2V	VGL+1,
	SI0	E1,
	S	E42,
E5=	SLN	E3	2	-- KOMMENTAR SOLL ERHALTEN BLEIBEN --
	SFB	ENZ,
	BA	'91',
	SI	E51,
	CH	VGL		-- KEIN KOMMENTAR --
	RT	AH,
	SFB	ANZ,
	BH	VGL,
	S	E2,
E51=	SFB	ENZ,
	BA	'91',
	SN	E51,
	SFB	ENZ,
	BA	'91',
	SN	E51,
E510=	BA	'AA'		--EINSETZEN KOMMA ANSTELLE KOMMENTAR --
	RT	AH,
E52=	SFB	ANZ		--GANZE INFORMATIONSEINHEIT IST UEBERTRAGEN--
E53=	SFB	ENZ,
	SL	E6	6	--ENDNUMMER DES BEREICHS IST UEBERLESEN--
	BA	'AF',
	SN	E22,
	S	E53		--LEERSTELLEN ZWISCHEN ENDE UND ANFANG ÜBERLESEN--
E6=	SLN	2	8	-- EINGANG MATERIALENDEMELDUNG VON S&GZF --
	S	TEZE		-- UP ENZ WURDE VOM ENTZERRER AUFGERUFEN --
	XB	CNL		-- AUSSTÜLPEN TEILGEFÜLLTEN PUFFER --
	B	NZP,
	VBA	'8000',
	SI0	E63		-- PUFFER IST LEER --
	R	B2V	B,
	XB	CNR,
	SI0	E61		-- LETZTES GANZWORT IST GEFÜLLT --
	MAB	VEL	0	-- EINBLENDEN ANZAHL BESETZTER OKTADEN --
	MAB	C	0,
E61=	VBA	NZP-2,
	SHB	R	1,
	TBC	VBINT+9		-- ANZAHL BESETZTER WORTE IM PUFFER --
	XBA	VBINT,
	SSR	253	10,
E63=	BA	2,
	XBA	VBINT,
	C2	VBINT+7,
	C	VBINT+10,
	SSR	253	12	-- UMSTEUERN BETRIEBSART LESEN --
	SSR	253	18	-- INF. ÜBER HÖCHSTE BELEGTE SATZPOSITION --
	SI0	(XBA 5, S TF1)	-- QUELLE IST LEER --
	SSR	253	14	-- BEENDEN DATEIBEARBEITUNG --
	SFB	S&GZFE,
	SLN	2	2	-- KEIN MODUS 0K --
	LZL	1	2,
	BZ	VBINT+2,
	XBA	1,
	CZ	VBGZFD+8,
	TBC	VBGZFD+13,
	BA	0,
	C	VBGZFD+4,
	SLN	TAW1	1	-- KEINE KOMPRESSION --
	XBA	'206',
	B	(NZP/AG, '1A'/H),
	TBC	VBGZFD+1		-- BEI KOMPRESSION DICHTGEPACKTE ABLAGE --
	C	VBGZFD+6,
TAW1=	BA	VBGZFD,
	SFB	S&GZFA,
	B	ZEILZAEHL		-- BERECHNEN UNGEFAEHRE LÄNGE ASUGABEDATEIEN --
	SLN	TAW2	1,
TAW10=	C2	VBTDAT+8,
	S	TAW5,
TAW2=	SLN	TAW3	4,
	BAR	7,
TAW21=	R	M2	H,
	S	TAW10,
TAW3=	BAR	3,
	S	TAW21,
TAW5=	TCB	(TAW7/A),
	TBC	VBTD2,
	XBA	VBTD2,
	SSR	253	19	--KENNDATEN--
	TCB	(S&SRF/A),
	TBC	VBTD2,
	B3	VBTD2+1		--DATEI EXISTIERT SCHON--
	SH	A	4,
	LA	F,
	SBA	1,
	SG0	TAW51,
	XBA	6,		--TDATEI IST VOM TYP SEQ--
	S	TF1,
TAW51=	SBA	2,
	SKG0	TAW510,
	XBA	7,
	S	TF1		-- TDATEI IST NICHT VOM TYP RAN ODER RAM --
TAW510=	BQ	('FFFFFF F0FFFF'/3),
	BT	VBTD2+8,
	BAR	3,
	SI	TAW52		--UNGEFAEHR FUER WORTZAHL--
	BA	2,
	SI	TAW512		--GENAU FUER WORTZAHL--
	B3	VBTD2+9		--MAXIMAL FUER WORTZAHL--
	BAR	13,
	SKG	TAW52,
TAW511=	XBA	8		--FORMAT G72O IM SATZ NICHT UNTERSTUETZT --
	S	TF1,
TAW512=	B3	VBTD2+9,
	BAR	13,
	SN	TAW511,
TAW52=	BA	6,
	XBA	VBTDAT,
	C2	VBTDAT+7		--BA SCHREIBEN--
	SSR	253	9,
	SSR	253	12,
	LC	VBTDAT+6,
	BZ	('1'/1, '3FFFFF FFFFFF'/1),
	CZ	VBTDAT+10,
	SSR	253	13	-- LOESCHEN ALLER SAETZE IM ZIEL --
TAW54=	LR	3A,
	C	VBTDAT+6,
	BA	0,
	C	VBTDAT+8,
	XBA	NZP,
	BA	13,
	TBC	VBTDAT+8,
	C2	VBTDAT+9,
	B	SNR,
	C	VBTDAT+10		-- SATZNUMMER BZW. KARTENNUMMER --
	SL	TKOMP	1,
	S	TENTZER,
TAW7=	QCR	QCR		--FEHLERAUSGANG SSR 253 19 --
	R	B3	A,
	BAR	'43',
	SI	TAW72		--DATEI EXISTIERT NICHT --
	QBR	QCR+10,
	S	S&SRF,
TAW72=	XBA	VBTDAT,
	SSR	253	3,
	BA	6,
	C	VBTDAT+6,
	SSR	253	9	--BA SCHREIBEN--
	LZL	3	0	-- K3: ZIELDATEI WURDE VON PS&TASAOP ERSTELLT --
	S	TAW54,
TKOMP=	SFB	S&GZF		-- TRANSPORT DIREKT IN AUSGABEPUFFER NZP --
	S	TEE,
	ZTR	1A,
	SBA	72,
	SI0	TK5,
TK1=	XB	CNR,
	VBA	NZPE,
	SXI	TK5,
	BA	'AF'		-- AUFFÜLLEN DER ZEILE MIT LEERZEICHEN --
	CNZ	CNL	CNR,
	S	TK1,
TK5=	SU	TAUS		-- AUSGABE DES PUFFERS --
	S	TKOMP,
TENTZER=	LZL	8	0	-- EINGANG ENTZERRER UND SCHÖNSCHREIBKOMPRESSOR --
	ZX	0	X0,
	BA	0,
	C	VGL,
	ZX	0	ZPF	-- PEGEL FUER INFORMATIONSKELLER IN AAP --
	XBA	AAP,
	VBA	2,
	XC	GWX,
	SU	TAELEER,
TAE21=	BA	'AF'		-- VORBESETZEN 1. 9 ZEICHEN MIT LEERSTELLEN --
	SFB	CNZ+1,
	XB	ZPF,
	VBA	9,
	SXK	TAE21,
TAE3=	XBA	AAP+16,
	XC	GWX,
	ZX	0	ZPF,
	LZL	0	6,
TAE30=	SFB	ENZ,
TAE31=	BA	'AA'		-- , --
	SI	TAE8,
	BA	'91'		-- - --
	SI	TAE5,
	BA	'97'		-- = --
	SI	TAE7,
	BA	'61'		-- ' --
	SI	TAE6,
	BA	'A0'		-- ( --
	SI	TAE4,
	BA	'AB'		-- : --
	SI	TAE57,
TAE32=	SFB	CNZ,
	S	TAE30,
TAE4=	SFB	CNZ		-- EINGEKLAMMERTE INFORMATION --
	BA	1,
	AC	VGL,
TAE40=	SFB	ENZ,
TAE41=	BA	'A0',
	SI	TAE4,
	BA	'A1',
	SN	TAE42,
	BA	1,
	SBC	VGL,
TAE42=	BA	'97'		-- = --
	SN	2R,
	LZL	6	0,
	BA	'61'		-- ' --
	SI	TAE44,
TAE43=	SFB	CNZ,
	B	VGL,
	SN0	TAE40,
	S	TAE30,
TAE44=	SFB	CNZ		-- STRING IM LITERAL --
	SFB	ENZ,
	BA	'61',
	SN	TAE41,
TAE45=	SFB	CNZ,
	SFB	ENZ,
	BA	'61',
	SN	TAE45,
	SFB	CNZ,
	SFB	ENZ,
	BA	'61',
	SN	TAE45,
	S	TAE43,
TAE5=	SFB	CNZ		-- KOMMENTAR --
	SFB	ENZ,
	BA	'91',
	SN	TAE31,
	SFB	CNZ,
	LC	VGL,
TAE50=	XB	ZPF,
	VBA	2,
	SLN	2	5,
	HBA	1,
	SXN	TAE51,
	LZL	6	0	-- KOMMENTAR BEGINNT IN SPALTE 1 --
	S	TAE56,
TAE51=	SLN	TAE56	4	-- SCHÖNSCHREIBKOMPRESSION --
	SL	TAE52	6	-- BENANNTE INFORMATIONSEINHEIT --
	VBA	11,
	SXGG	TAE56		-- KOMENTAR BEGINNT NACH SPALTE 21 --
	ZX	10	XVG,
TAE510=	SLN	4	5,
	HXP	-1	ZPF,
	HXP	-2	GWX,
	S	TAE511,
	HXP	-2	ZPF,
	HXP	-4	GWX,
TAE511=	BA	'AF',
	SFB	CNZ+1,
	XB	ZPF,
	HBPX	-1	XVG,
	SXKG	TAE511,
	S	TAE53,
TAE52=	VBA	20,
	SXGG	TAE56,
	ZX	19	XVG,
	S	TAE510,
TAE53=	SLN	TAE54	5,
	BA	'AB',
	RT	AH,
TAE530=	SFB	CNZ,
TAE531=	SFB	ENZ,
	BA	'AC',
	SN	TAE530,
	S	TAE561,
TAE54=	BA	'91',
	SFB	CNZ+1,
	BA	'91',
	RT	AH,
TAE55=	SFB	CNZ,
TAE551=	SFB	ENZ,
	BA	'91',
	SN	TAE55,
	SFB	CNZ,
	SFB	ENZ,
	BA	'91',
	SN	TAE55,
TAE561=	SFB	CNZ,
	LZL	0	5,
	S	TAE81,
TAE56=	SLN	TAE551	5,
	S	TAE531,
TAE57=	SFB	CNZ		-- KOMMENTARBEGINN IST : --
	LZL	5	0,
	S	TAE50,
TAE6=	SFB	CNZ		-- STRING --
	SFB	ENZ,
	BA	'61',
	SN	TAE31,
TAE61=	SFB	CNZ,
	SFB	ENZ,
	BA	'61',
	SN	TAE61,
	SFB	CNZ,
	SFB	ENZ,
	BA	'61',
	SN	TAE61,
	S	TAE32,
TAE7=	LZL	6	0	-- ZEICHEN = STEHT AN --
	SLN	TAE73	4,
TAE71=	XB	ZPF,
	VBA	7,
	SXN	TAE72,
	BA	'97',
	SFB	CNZ+1		-- ZEICHEN = NACH SPALTE 8 --
	BA	'AF',
	SFB	CNZ+1,
	SFB	ENZ,
	BA	'AF',
	SN	TAE31,
	S	TAE30,
TAE72=	SXG	TAE73		-- SPALTE 8 ODER MEHR SCHON BESETZT --
	BA	'AF',
	SFB	CNZ+1,
	S	TAE71,
TAE73=	SFB	CNZ,
	S	TAE30,
TAE8=	SFB	CNZ,
TAE81=	SL	TAE82	64	-- ENTZERRER LÄUFT ODER INF. BEGINNT IN SPALTE 1 --
	XB	ZPF,
	HBPX	-1	ZJK,
	SXG	TAE82		-- INF. GEHT NICHT IN ANGEFANGENE KARTE --
	SL	TAE830	2	-- INFORMATION KOMMT IN EINE NEUE KARTE --
TAE811=	XBA	AAP+16,
	S	TAE84,
TAE82=	SL	TAE83	2	-- PUFFER NZP IST LEER --
	SU	TAUS,
	SU	TAELEER,
TAE83=	SL	TAE811	6	-- INF BEGINNT IN SPALTE 1 --
TAE830=	SL	TAE831	4	-- ENTZERRER LÄUFT --
	HXP	2	ZPF,
	XBA	AAP+12,
	S	TAE84,
TAE831=	HXP	9	ZPF,
	XBA	AAP,
	VBA	2,
TAE84=	XC	X3,
TAE841=	EZ	B	X3,
	HBPX	-1	GWX,
	SXG	TAE85		-- INFORMATION IST NACH NZP ÜBERTRAGEN --
	SU	TANZ,
	S	TAE841,
TAE85=	XB	ZPF,
	VBA	72,
	SXKG	TAE86		-- INF. WAR KLEINER ALS 73 ZEICHEN --
	SL	TAE86	2	-- PUFFER IST LEER --
	SU	TAUS,
	SU	TAELEER,
TAE86=	SL	TEZ1	1,
	S	TAE3,
TEZE=	LZL	1	0	-- EINGABEDATEI IST VOLLSTAENDIG GELESEN --
	XB	ZPF,
	SXN	TAE81		-- IN AAP STEHT NOCH INFORMATION --
TEZ1=	SL	TEE	2	-- PUFFER IST LEER --
	SU	TAUS,
TEE=	SU	TEA,
	XBA	VB14,
	SSR	0	12	-- OPENDE --

--- UNTERPROGRAMME --
DATPAR=	AA	STI		-- UP BRINGT DATEIKENNGRÖSSEN, <A>=>LISTENBESCHR.< --
	R	MCFU	A,
	B3	1,
	AA	STI,
	RT	AQ		-- LEISTUNG: <H>=DATENBASISNAME, <A>= AADTB,LNGDTB --
	R	MCFU	Q,
	B3	1,
	SBA	'1000',
	AA	STI		-- >DATENBASISNAME< --
	R	MCFU	A,
	BH	0		-- DATENBASISNAME --
	R	MCF	Q,
	B2	2,
	LR	3Q,
	SH	ZU	12,
	LA	T,
	RT	AQ,
	SH	ALK	12,
	AA	STI		-- >DATEINAME< --
	SH	AK	24,
	R	VEL	Q,
	RT	AQ,
	MAB	B3	3,
	SI0	5R,
	SBA	'1000',
	AA	STI,
	R	MCFU	A,
	B	0,
	RT	AQ,
	MU	S	0,
TAUS=	XBA	VBTDAT,
	LC	NZPE,
	SSR	253	11,
	RT	AD,
	SI0	2R,
	S	F11,
	SSR	253	10,
TAU4=	B	SW,
	A	VBTDAT+10		-- ADDITION SCHRITTWEITE ZUR SATZ-BZW.KARTENNUMMER--
	C	VBTDAT+10,
	MU	S	0,
ENZ=	TBC	TV1		-- UP LIEST EIN ZEICHEN AUS ZEILENPUFFER--
	XB	X0,
	SXK	ENZ1		-- ES SIND NOCH ZEICHEN VORHANDEN --
ENZG=	SFB	S&GZF,
	S	E6,
	TRX	AN	X0,
	SXI	ENZG		-- LEERER SATZ WURDE ÜBERGEBEN --
	AA	ZEIPU,
	TRX	A	X1	-- EA+1 DER HALBWORTE MIT GÜLTIGEN OKTADEN --
	SL	ENZ0	8	-- LESEN AUS INTERNDATEI QUELLE --
	ZTR	3H,
	B	ESNR,
	SGG	ENZ0		-- KLEINER ODER GLEICH ENDNUMMER DES BEREICHES --
	LZL	6	0	-- ENDNUMMER WURDE SCHON GELESEN --
ENZ0=	B2	ZEIPU,
	S	ENZ2,
ENZ1=	EMB	B2	X1,
ENZ2=	HXP	1	X0,
	BAR	64,
	SG	ENZ+2		-- STEUERZEICHEN ELIMINIEREN --
	SE	TV1,
ANZ=	TBC	TV2,
	RT	AH,
	CNZ	CNL	CNR	-- ABSPEICHERN KOMPRIMIERTE ZEILE --
	XB	CNR,
	VBA	NZPE,
	SXK	ANZ3,
	XBA	VBINT,
	SSR	253	10	-- AUSSTÜLPEN PUFFER --
	HXP	-2	CNR,
	LR	3A,
ANZ2=	ENZ	C	CNR	-- LÖSCHEN PUFFER --
	VBA	NZP-2,
	SXG	ANZ2,
	HXP	2	CNR,
	BA	1,
	AC	ZEILZAEHL,
ANZ3=	SE	TV2,
CNZ=	RT	AH		-- UP ZUM ABLEGEN EINES ZEICHENS IN AAP --
	TBC	TV3,
	EZ	C	GWX,
	HXP	1	ZPF,
	VBA	1015,
	SXGG	(XBA 9, S TF1)	--ZU LANGE INFORMATIONSEINHEIT --
	SE	TV3,
TANZ=	CNZ	CNL	CNR	-- UP GIBT ZEICHEN IN ZEILENPUFFER AUS --
	HXP	-1	ZJK,
	LZL	0	2,
	MU	SXG	0,
	SU	TAUS,
	SU	TAELEER,
	MU	S	0,
TAELEER=	B	LEER		-- UP FÜLLT ZEILENPUFFER MIT LEERZEICHEN --
	XBA	NZP,
	C	NZP,
	XC	CNR,
	XBA	NZP+2,
	XC	CNL,
	XBA	11,
	WTV	CNL	CNR,
	XBA	'8000',
	XC	CNL,
	LZL	2	0	-- K2 GESETZT: PUFFER IST LEER --
	ZX	72	ZJK	-- ZEICHENZAEHLER --
	MU	S	0,
TEA=	SU	TDCLOSE,
	SU	TDLOESCH,
	BA	(''KOMPRIMIERE'',''001300''),
	SFB	S&OPZEIT+2,
	MU	S	0,
TDCLOSE=	SFB	S&GZFE		-- UP BEENDET ALLE DATEIBEARBEITUNGEN --
	B2	(TFC1/A),
	XBA	VBINT,
	C2	VBINT,
	SSR	253	14,
TFC1=	B2	(TDC4/A),
	XBA	VBTDAT,
	C2	VBTDAT,
	SSR	253	14,
TDC4=	TCB	VB14,
	TBC	VBINT,
	TBC	VBTDAT,
	SLN	TDC5	3	-- ZIELDATEI WURDE NICHT VON PS&TASAOP ERSTELLT --
	BA	0,
	XBA	VBTDAT,
	C2	VBTDAT+1,
	SSR	253	26	-- AUFGEBEN FREIEN DATEISPEICHER --
TDC5=	MU	S	0,
TDLOESCH=	B2	(TDL3/A)		-- UP ZUM LOESCHEN ALLER INTERNDATEIEN --
	XBA	VBINT,
	C2	VBINT,
	SSR	253	4,
TDL3=	MU	S	0,

--- FEHLERBEHANDLUNG --
TAF1=	QCR	QCR,
	SXN	TF3		-- BENUTZERFEHLER --
TAF2=	XBA	VB60,
	SSR	6	0	-- DUMP LAUFZEITGEBIETE --
	S	TF3,
TF1=	R	MCF	B	-- BENUTZERFEHLER INFOLGE FALSCHER VERSORGUNG --
	T	TBA-1,
	BA	8,
	TBC	VB612+2,
	C2	VB612+3,
TF2=	BA	3,
	XBA	VB612,
	C2	VB612+1,
	SSR	6	16,
TF3=	SU	TEA,
	XBA	VB016,
	KE	FEHL,
	SSR	0	16,
TAF3=	SI0	TF3		--ERG.ALARM--
	S	TAF2,
TAF4=	XBA	1,
	TBC	VB016+3,
	S	TF3,
F11=	MAB	BR	4	-- SSR 253 11: RUECKMELDUNG UNGLEICH 0 --
	R	TCB	AL	-- >DATEINAME< --
	VBA	2,
	XC	X0,
	R	TCB	A	-- LAENGE DATEINAME --
	VBA	1,
	XCN	X1,
	R	MCFU	H,
	T	RMTEXT-1,
	BA	13,
	TBC	VB612+2,
	C2	VB612+3,
	HBA	2,
	XC	X3,
F12=	EZ	B	X0,
	EZ	C	X3,
	SZX	F12	X1,
	S	TF2,

--- KONSTANTE VERSORGUNGSBLÖCKE --
VB14=		S&SRF/AG, 0/H, STI/A, 256/H,
VB60=		S&SRF/AG, 3/H, 0/2,

--- KONSTANTEN --
TBA=	XBA	TBF1/K,
	XBA	TBF2/K,
	XBA	TBF3/K,
	XBA	TBF4/K,
	XBA	TBF5/K,
	XBA	TBF6/K,
	XBA	TBF7/K,
	XBA	TBF8/K,
	XBA	TBF9/K,
RMTEXT=	XBA	TBFRM1/K,
	XBA	TBFRM2/K,
	XBA	TBFRM3/K,
	XBA	TBFRM4/K,
LEER=		'AFAFAF AFAFAF'/3,
QNAM=		''*QUEL*'',

--- FEHLERTEXTE --
TBF1=	''*019FEHLER: UNZUL. PARAMETER BEI SPEZ. SPRACHE     '',
TBF2=	''*019FEHLER: UNZUL. PARAMETER BEI SPEZ. MODUS.      '',
TBF3=	''*019FEHLER: UNZUL. PARAMETER BEI SPEZ. BEREICH.  '',
TBF4=	''*019FEHLER:EINGABEDATEI UND ZIEL SIND IDENTISCH.   '',
TBF5=	''*019FEHLER:QUELLE IST LEER.                        '',
TBF6=	''*019FEHLER: DATEITYP SEQ BEI ZIELDATEI UNZULAESSIG.'',
TBF7=	''*019FEHLER: ZIELDATEI HAT NICHT TYP RAN ODER RAM.'',
TBF8=	''*019FEHLER: SATZBAU G72 O GEHT NICHT IN ZIELDATEI. '',
TBF9=	''*019FEHLER:INFORMATIONSEINHEIT IST > 1024 ZEICHEN.  '',

--- VARIABLE --

AAP=	ASP	4K4,
XB=	ASP	256/G,
VB612=	S&SRF/AGV, '1'/VH, TF1/AV, '3'/HV,
VB016=	TAF4/AVG, '0'/HV, 2/V,
SNR=	10/V, 
SW=	10/V,
ZEILZAEHL=	'1'/AV,
VGL=	ASP	2/G,
ZEIPU=	ASP	160/G,
NZP=	ASP	24/G,
NZPE=	'0'/3V,
	ASP	2/G,
STI.=	ASP	512/G,
TV1=	ASP	1/G,
TV2=	ASP	1,
TV3=	ASP	1,
TV4=	ASP	1,
ESNR=	'FFFFFF FFFFFF'/3V,
QCR=ASP	12/G,

--- VARIABLE VERSORGUNGSDBLÖCKE --
VBINT=	S&SRF/AGV, '002020'/HV,
	''&STDDB''/V, QNAM/AV, '1'/HV,
	'1'/3V,
	'100'/3HV,
	'33000E'/3HV,
	'0'/2V, '0'/2V, '0'/3V, '1'/HV, '3'/HV,
	'100'/HV, 'A01'/HV,
VBTDAT=	S&SRF/AGV, '002030'/HV,
	'0'/V, '0'/V,'1'/3V, '0'/HV3,
	'33000E'/HV,
	'0'/2V, '0'/2V, '0'/3V, '1'/HV, '3'/HV,
	'100'/HV, 'A01'/HV,
VBTD2=	S&SRF/AGV, '002000'/HV,
	ASP	24/G,
VB08=	S&SRF/AGV, STI+2/AV, 0/3V,
	0/V, 0/V, 0/V, 0/V, 0/V,
VBGZFG=	S&SMFEHL/AVG, '010007'/VH, 
	1/HVG, 1/HV, '0'/HV, '4'/HV,
	ZEIPU/AV, 160/HV,
	ASP	6/G,
	AAP/AVG, '0'/HV,
VBGZFP=	S&SMFEHL/AVG, '010103'/HV,
	1/HVG, 1/HV, 404/V,
	ZEIPU/AVG, 160/HV,
VBGZFD=	S&SMFEHL/AVG, '010206'/HV,
	1/HGV, 1/HV, '0'/1V,
	ZEIPU/AVG, 160/HV,
	ASP	4/G,
	1/V, 0/V,

--- VARIABLE TEXTE --

TBFRM1=	''*021AUS DATEI ''/V, 0/3V, 0/3V, 0/3V,
	''SOLLTE EIN UNDEFINIERTER SATZ GELESEN WERDEN''/V,
TBFRM2=	''*021BEI DATEI ''/V, 0/3V, 0/3V, 0/3V,
	''LESEVERSUCH AUSSERHABL DES DEFINIERTEN BEREICHS''/V,
TBFRM3=	''*021BEI DATEI ''/V, 0/3V, 0/3V, 0/3V,
	''UEBERSCHREITUNG DER MAX. ZUGEL. SATZPOSITION''/V,
TBFRM4=	''*021BEI DATEI ''/V, 0/3V, 0/3V, 0/3V,
	''UNKORREKTE POSITIONIERUNG BEI SEQ-DATEI''/V,
	0/3V,

	ENDE,
